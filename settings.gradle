include ':app'

// =======
// = Lib =
// =======

def libs = [
        "DevApp",
        "DevAssist",
        "DevBase",
        "DevBaseMVVM",
        "DevEngine",
        "DevHttpCapture",
        "DevHttpManager",
        "DevJava",
        "DevWidget",
]

libs.forEach {
    include(":$it")
    project(":$it").projectDir = new File(rootDir, "lib/$it")
}

def libs_compiler = [
        "DevHttpCaptureCompiler",
        "DevHttpCaptureCompilerRelease",
]

libs_compiler.forEach {
    include(":$it")
    if (it.startsWith("DevHttpCapture")) {
        project(":$it").projectDir = new File(rootDir, "lib/HttpCapture/$it")
    }
}

def environment_libs = [
        "DevEnvironment",
        "DevEnvironmentBase",
        "DevEnvironmentCompiler",
        "DevEnvironmentCompilerRelease",
]

environment_libs.forEach {
    include(":$it")
    project(":$it").projectDir = new File(rootDir, "lib/Environment/$it")
}

// ===============
// = Libs Module =
// ===============

def libs_modules = [
        "DevBase2",
        "DevOther",
]

libs_modules.forEach {
    include(":$it")
    project(":$it").projectDir = new File(rootDir, "lib/$it")
}

// ==========
// = Module =
// ==========

def modules = [
        "DevBaseModule",
        "push/DevGTPush",
        "push/DevJPush",
        "share/DevUMShare",
]

modules.forEach {
    def itName = it

    if (itName.indexOf("/") != -1) {
        itName = itName.substring(
                itName.lastIndexOf("/") + 1,
                itName.length()
        )
    }
    include(":$itName")
    project(":$itName").projectDir = new File(rootDir, "module/$it")
}

// ===========
// = Project =
// ===========

def projects = [
        "AccessibilityKtx",
        "AppInfoKtx",
        "DemoKtx",
        "GTPushKtx",
        "JPushKtx",
        "UMShareKtx",
]

projects.forEach {
    include(":$it")
    project(":$it").projectDir = new File(rootDir, "project/$it")
}

// ===============
// = Interesting =
// ===============

def interestings = [
        "DevScreenMatch",
        "DevStandard",
]

interestings.forEach {
    include(":$it")
    project(":$it").projectDir = new File(rootDir, "interesting/$it")
}