// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply from: rootProject.file("file/gradle/config.gradle")

buildscript {

    // Kotlin 版本
    ext.kotlin_version = "1.5.20"
    ext.kotlin_stdlib = "1.5.20"
    ext.matrix_version = "0.9.0"

    repositories { // 该 repositories 用于 buildscript dependencies 插件、脚本依赖加载
        // AliRepo 阿里仓库服务 https://maven.aliyun.com/mvn/view
        maven { url "https://maven.aliyun.com/repository/google" }
        maven { url "https://maven.aliyun.com/repository/public" }
        maven { url "https://maven.aliyun.com/repository/gradle-plugin" }

        google()

        // jitpack maven
        maven { url "https://jitpack.io" }

        maven { url "https://plugins.gradle.org/m2" }

        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }

        maven { url "https://repo1.maven.org/maven2/" }

        // 个推 maven
        maven { url "https://mvn.getui.com/nexus/content/repositories/releases/" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.0'
        // kotlin https://mvnrepository.com/artifact/org.jetbrains.kotlin/kotlin-gradle-plugin
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // ARouter AutoRegister
        classpath "com.alibaba:arouter-register:1.0.2"
        // greendao
        classpath "org.greenrobot:greendao-gradle-plugin:3.3.0"
        // Hilt 依赖注入
        classpath "com.google.dagger:hilt-android-gradle-plugin:2.38"
        // android maven https://plugins.gradle.org/plugin/com.github.dcendents.android-maven
        classpath "com.github.dcendents:android-maven-gradle-plugin:2.1"
        // bintray https://plugins.gradle.org/plugin/com.jfrog.bintray
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5"
        // https://mvnrepository.com/artifact/org.jetbrains.dokka/dokka-gradle-plugin
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:1.4.32"
        // https://mvnrepository.com/artifact/com.tencent.matrix/matrix-gradle-plugin
        classpath("com.tencent.matrix:matrix-gradle-plugin:$matrix_version") { changing = true }
    }
}

allprojects {
    repositories { // 该 repositories 用于 app、module dependencies 第三方库 implementation 等依赖加载
        google()

        // jitpack maven
        maven { url "https://jitpack.io" }

        maven { url "https://plugins.gradle.org/m2" }

        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }

        maven { url "https://repo1.maven.org/maven2/" }

        // 个推 maven
        maven { url "https://mvn.getui.com/nexus/content/repositories/releases/" }
    }

//    gradle.projectsEvaluated {
//        tasks.withType(JavaCompile) {
//            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
//        }
//    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// 全局编码设置
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// =================
// = config.gradle =
// =================

import groovy.json.JsonOutput

// 存储 config.gradle json 数据
task saveConfigGradleJSON() {
//    println deps.toString()
    // 获取 ext.deps json 数据
    def depsJSON = JsonOutput.toJson(deps)
    // 格式化 JSON
    def formatJSON = JsonOutput.prettyPrint(depsJSON)
    // 写入 ext.deps 配置信息
    def file = new File(rootDir, "/file/json/deps.json")
    file.withPrintWriter { it.print(formatJSON) }
}

// 生成 deps dependencies gradle
task generateDepsDependencies() {
    def newline = System.getProperty("line.separator")
    def newline2 = newline + newline
    def importSymbol = "implementation"
    // 拼接 string
    def builder = new StringBuilder()
    builder.append("dependencies {")
    for (String module : deps.keySet()) {
        builder.append("${newline2}\t// = ${module} =${newline}")
        // 循环拼接处理
        def valueMaps = deps.get(module) as LinkedHashMap<String, String>
        for (String name : valueMaps.keySet()) {
            def value = valueMaps.get(name)
            builder.append("${newline}\t// ${name}")
            builder.append("${newline}\t${importSymbol} '${value}'")
        }
    }
    builder.append("${newline}}")
    // 写入 ext.deps 配置信息
    def file = new File(rootDir, "file/json/deps_gradle") // deps.gradle 防止每次生成显示 gradle sync
    file.withPrintWriter { it.print(builder.toString()) }
}